#include "AmnesiaSignatures.cpp"
#include "ResetPlayerStateOnStart.ihps"
#include "AmnesiaSignatures.cpp"
#include "AmnesiaExtensions.ihps"
#include "Utilities.ihps"
#include "EveryMapEvents_AUTORUN.ihps"
#include "ForceEssentialItems_AUTORUN.ihps"

// ============================================================================
// USED ENTITIES
// Constants representing entities referenced inside this script
// in order to prevent misspelling and allow for easier renaming.
// - For consistency use an "ent" prefix.
// ============================================================================
const string entRisenSalt           = "RisenSalt";
const string entRisenWater          = "RisenWater";

// ----------------------------------------------------------------------------
// Main - g
// ----------------------------------------------------------------------------
const string gMapId = "10";

void OnStart()
{
    FadeOut(0.0f);
    StopPlayerLookAt();
    WakeUpDialog();
    SetPlayerMoveSpeedMul(0.0f);
    SetPlayerRunSpeedMul(0.0f);
    BlockAllVanillaHints();
    RegisterCallbacks();
}

void OnLeave()
{
}

void RegisterCallbacks()
{
    AddSinglePlayerCollideCallback("JohanLineArea", "StartDialog");
    AddSinglePlayerCollideCallback("PrioryLineArea", "StartPriory");
    AddSinglePlayerCollideCallback("LeftHandPathArea", "StartHorror");
}

// ----------------------------------------------------------------------------
// After-Cutscene Dialog
// ----------------------------------------------------------------------------

void WakeUpDialog(string t)
{
    int stage = GetLocalVarInt("WakeUpDialog");
    AddLocalVarInt("WakeUpDialog", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        FadeIn(8.0f);

        delay = 5.0f;
    }
    else if(stage == 1)
    {
        PlayVoiceOnPlayer(
            "RC_Johan_4.ogg", 
            "JohanNarration", 
            "35"
        );
        SetEffectVoiceOverCallback("WakeUpDialog");
        return;
    }
    else if(stage == 2)
    {
        PlayVoiceOnPlayer(
            "RC_Johan_5.ogg", 
            "JohanNarration", 
            "36"
        );
        SetEffectVoiceOverCallback("WakeUpDialog");
        return;
    }
    else if(stage == 3)
    {
        ResetPlayerSpeed();
    }
    else
    {
        return;
    }

    AddTimer("", delay, "WakeUpDialog");
}

void WakeUpDialog()
{
    WakeUpDialog("");
}

// ----------------------------------------------------------------------------
// Introducing the Item Quest (Should be expanded to tie into the area box by the torture room door)
// ----------------------------------------------------------------------------

void StartDialog(string a, string b, int s)
{
    QuestDialog();
}

void QuestDialog(string t)
{
    int stage = GetLocalVarInt("QuestDialog");
    AddLocalVarInt("QuestDialog", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        PlayVoiceOnPlayer(
            "RC_Johan_6.ogg", 
            "JohanNarration", 
            "37"
        );
        SetEffectVoiceOverCallback("QuestDialog");
        return;
    }
    else if(stage == 1)
    {
        PlayVoiceOnPlayer(
            "RC_Johan_6b.ogg", 
            "JohanNarration", 
            "37b"
        );
        SetEffectVoiceOverCallback("QuestDialog");
        return;
    }
    else
    {
        return;
    }

    AddTimer("", delay, "QuestDialog");
}

void QuestDialog()
{
    QuestDialog("");
}

// ----------------------------------------------------------------------------
// PRIORY CODE AREA
// ----------------------------------------------------------------------------

void StartPriory(string a, string b, int s)
{
    PrioryDialog();
}

void PrioryDialog(string t)
{
    int stage = GetLocalVarInt("PrioryDialog");
    AddLocalVarInt("PrioryDialog", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        PlayVoiceOnPlayer(
            "RC_Johan_7.ogg", 
            "JohanNarration", 
            "38"
        );
        SetEffectVoiceOverCallback("PrioryDialog");
        return;
    }
    else
    {
        return;
    }

    AddTimer("", delay, "PrioryDialog");
}

void PrioryDialog()
{
    PrioryDialog("");
}

// ============================================================================
// COLLECTING REQUIRED ITEMS
// ============================================================================
const int requiredItemsCount = 2;

bool ItemIsRequired(string itemName)
{
    return itemName == entRisenWater ||
    itemName == entRisenSalt;
}

void OnRequiredItemPickup(string entity)
{
    // Ignore not required items
    if (!ItemIsRequired(entity)) { return; }

    if (entity == entRisenWater)        { OnWaterPickedUp(); }
    if (entity == entRisenSalt)         { OnSaltPickedUp(); }

    AddLocalVarInt("RequiredItemsPickedUp", 1);
    GiveQuestIfNotAdded("RisenQuest");

    if (AllRequiredItemsPickedUp())
    {
        OnAllItemsPickedUp();
    }
}

bool AllRequiredItemsPickedUp()
{
    return GetLocalVarInt("RequiredItemsPickedUp") == requiredItemsCount;
}

// ============================================================================
// REQUIRED ITEMS PICK UP EVENTS
// ============================================================================

void OnWaterPickedUp()
{
        PlayVoiceOnPlayer(
            "RC_Johan_10.ogg", 
            "JohanNarration", 
            "41"
        );
}

void OnSaltPickedUp()
{
        PlayVoiceOnPlayer(
            "RC_Johan_12.ogg", 
            "JohanNarration", 
            "43"
        );
}

void OnAllItemsPickedUp()
{
    CompleteQuest("RisenQuest");
    AddSinglePlayerCollideCallback("EndingCutsceneTrigger", "StartEnding");
}

void StartEnding(string a, string b, int s)
{
    PlayVoiceOnPlayerAndSlowdown("RC_Johan_13.ogg");
    SetSwingDoorLocked("BlowOpenDoor1", false, true);
}


// ============================================================================
// LEFT HAND PATH HORROR AREAS
// ============================================================================

void StartHorror(string a, string b, int s)
{
    PlayVoiceOnPlayerAndSlowdown("RC_Johan_11.ogg");
}