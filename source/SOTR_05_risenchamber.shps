#include "Ramlord.ihps"

// ============================================================================
// USED ENTITIES
// Constants representing entities referenced inside this script
// in order to prevent misspelling and allow for easier renaming.
// - For consistency use an "ent" prefix.
// ============================================================================
const string entRisenSalt           = "RisenSalt";
const string entRisenWater          = "RisenWater";
const string entAntonArea           = "AntonVoiceArea";
const string entTortureFocus        = "TortureFocusPoint";
const string entShakeEventArea      = "ShakeEventArea";
const string entBrickFallBase       = "BrickFall_";
const int entBrickFallCount         = 6;
const string entBrickFallParticleBase  = "BrickFallParticle_";
const int entBrickFallParticleCount = 6;
const string entPianoArea           = "PianoSoundArea_1";
const string entHallwayEventArea    = "HallwayEventArea";
const string entHallwayLookAt       = "HallwayLookAtArea";

const string psDustFalling = "ps_dust_falling_rumble.ps";

const string sntKnifeSound = "24_knife.snt";
const string sntCut        = "24_cut.snt";
const string sntChain      = "24_chain_rattle.snt";
const string sntRumble     = "18_pump_rumble.snt";
const string sntPiano      = "general_piano01.snt";
const string sntTerror = "ui_terror_meter.snt";
const string sntBell = "ui_torture.snt";
const string sntInject = "19_inject.snt";
const string sntCloth = "player_stand.snt";
const string sntCrouch = "player_crouch.snt";

const string oggIntro = "24_event_vision.ogg";
const string oggDying = "24_event_vision03.ogg";
const string oggHallway = "00_event_hallway.ogg";

// ----------------------------------------------------------------------------
// Main - g
// ----------------------------------------------------------------------------
const string MapId = "05";

void OnStart()
{
    FadeOut(0.0f);
    StopPlayerLookAt();
    TortureDialog();
    SetPlayerMoveSpeedMul(0.0f);
    SetPlayerRunSpeedMul(0.0f);
    RegisterCallbacks();
}

void RegisterCallbacks()
{
    AddSinglePlayerCollideCallback("JohanLineArea", "StartDialog");
    AddSinglePlayerCollideCallback("PrioryLineArea", "StartPriory");
    AddSinglePlayerCollideCallback("LeftHandPathArea", "StartHorror");
    AddSinglePlayerCollideCallback(entShakeEventArea, "ShakeEvent");
    AddSinglePlayerCollideCallback(entHallwayEventArea, "HallwayEvent");
}
// ============================================================================
// HALLWAY EVENT
// ============================================================================
void HallwayEvent(string a, string b, int s)
{
    HallwayEvent("");
    AutoSave();
}

void HallwayEvent(string t)
{
    int stage = GetLocalVarInt("HallwayEvent");
    AddLocalVarInt("HallwayEvent", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        StopHeadTilt();
        PlayGuiSound(oggHallway, 1.0f);
        FadePlayerFOVMulTo(1.5f, 0.5f);
        SetPlayerMoveSpeedMul(0.2f);
        SetPlayerRunSpeedMul(0.2f);
        StartPlayerLookAtNormal(entHallwayLookAt);
        delay = 6.5f;
    }
    else if(stage == 1)
    {
        FadePlayerFOVMulTo(1.0f, 1.0f);
        delay = 5.0f;
    }
    else if(stage == 2)
    {
        ResetPlayerSpeed();
        StopPlayerLookAt();
        StartHeadTilt();
    }
    else
    {
        return;
    }

    AddTimer("", delay, "HallwayEvent");
}

// ============================================================================
// HEAD TILT
// ============================================================================
void HeadTilt(string t)
{
    if(GetLocalVarInt("PauseHeadTilt") == 1) { return; }
    FadePlayerRollTo(RandFloat(-10.0f, 10.0f), 1.0f, 1.5f);
    FadePlayerFOVMulTo(RandFloat(0.9f, 1.0f), 0.2f);
    AddTimer("tilt", RandFloat(2.0f, 4.0f), "HeadTilt");
}

void StopHeadTilt()
{
    SetLocalVarInt("PauseHeadTilt", 1);
    RemoveTimer("tilt");
}

void StartHeadTilt()
{
    SetLocalVarInt("PauseHeadTilt", 0);
    HeadTilt("");
}

// ============================================================================
// Shake Event
// ============================================================================
void ShakeEvent(string a, string b, int s)
{
    ShakeEventTimer();
}

void ShakeEventTimer(string t)
{
    int stage = GetLocalVarInt("ShakeEventTimer");
    AddLocalVarInt("ShakeEventTimer", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        GiveSanityDamage(1.0f, true);
        SetLampLit("BrickTorch_1", false, true);
        StartScreenShake(0.07f, 3.0f, 0.1f, 0.1f);
        PlaySoundAtEntity("rumble", sntRumble, "Player");
        delay = 0.2f;
    }
    else if(stage == 1)
    {
        SpawnBrick(1);
        SpawnBrick(2);
        delay = 0.5f;
    }
    else if(stage == 2)
    {
        SpawnBrick(3);
        delay = 0.3f;
    }
    else if(stage == 3)
    {
        SpawnBrick(4);
        delay = 0.1f;
    }
    else if(stage == 4)
    {
        SpawnBrick(5);
        delay = 0.5f;
    }
    else if(stage == 5)
    {
        SpawnBrick(6);
        delay = 0.3f;
    }
    else
    {
        StopSound("rumble", 1.0f);
        return;
    }

    AddTimer("", delay, "ShakeEventTimer");
}

void ShakeEventTimer() { ShakeEventTimer(""); }

void SpawnBrick(int number)
{
    SetEntityActive(entBrickFallBase + number, true);
    CreateParticleSystemAtEntity(psDustFalling, entBrickFallParticleBase + number);
}

// ----------------------------------------------------------------------------
// Cutscene Dialog
// ----------------------------------------------------------------------------
void TortureDialog(string t)
{
    int stage = GetLocalVarInt("TortureDialog");
    AddLocalVarInt("TortureDialog", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        FadeGlobalSoundVolume(0.0f, 0.0f);
        TeleportPlayer("PlayerStartArea_2");
        SetPlayerActive(false);
        // <DEBUG>
        //SetLocalVarInt("TortureDialog", 22);
        // </DEBUG>
        delay = 0.1f;
    }
    else if(stage == 1)
    {
        MovePlayerHeadPos(0.0f, -0.4f, 0.0f, 20.0f, 0.5f);
        FadePlayerRollTo(-15.0f, 20.0f, 20.0f);
        StartPlayerLookAtNormal(entTortureFocus);
        SetPlayerActive(false);
        delay = 1.9f;
    }
    else if(stage == 2)
    {
        PlayVoice("RC_Johan_1.ogg");
        SetEffectVoiceOverCallback("TortureDialog");
        return;
    }
    else if(stage == 3)
    {
        FadeIn(0.5f);
        FadeGlobalSoundVolume(1.0f, 0.5f);
        PlayMusic(oggIntro, true, 0.1f, 1.0f, 1, false);
        PlayVoice("RC_Johan_2.ogg");
        SetEffectVoiceOverCallback("TortureDialog");
        return;
    }
    else if(stage == 4)
    {
        delay = 3.0f;
    }
    else if(stage == 5)
    {
        SetMoveObjectStateExt("castle_portcullis_1", 1.0f, 0.8f, 1.0f, 0.5f, false);
        StopMusic(1, 3.0f);
        delay = 8.0f;
    }
    else if(stage == 6)
    {
        PlaySoundAtEntity("", "step_walk_rock.snt", "Footstep_1", 0.1f, false);
        delay = 1.5f;
    }
    else if(stage == 7)
    {
        PlaySoundAtEntity("", "step_walk_rock.snt", "Footstep_2", 0.1f, false);
        delay = 1.5f;
    }
    else if(stage == 8)
    {
        PlaySoundAtEntity("", "step_walk_rock.snt", "Footstep_3", 0.1f, false);
        delay = 1.5f;
    }
    else if(stage == 9)
    {
        PlaySoundAtEntity("brandish", sntKnifeSound, entAntonArea, 0.1f, true);
        PlaySoundAtEntity("", "step_walk_rock.snt", "Footstep_4", 0.1f, false);
        delay = 1.5f;
    }
    else if(stage == 10)
    {
        PlaySoundAtEntity("", "step_walk_rock.snt", "Footstep_5", 0.1f, false);
        delay = 4.5f;
    }
    else if(stage == 11)
    {
        PlayVoice("RC_Anton_1.ogg", "AntonVoiceArea");
        SetEffectVoiceOverCallback("TortureDialog");
        return;
    }
    else if(stage == 12)
    {
        delay = 2.0f;
    }
    else if(stage == 13)
    {
        PlayVoice("RC_Anton_2.ogg", "AntonVoiceArea");
        SetEffectVoiceOverCallback("TortureDialog");
        return;
    }
    else if(stage == 14)
    {
        PlayVoice("RC_Anton_3.ogg", "AntonVoiceArea");
        SetEffectVoiceOverCallback("TortureDialog");
        return;
    }
    else if(stage == 15)
    {
        ///ROOM FOR SLASHING SOUND/IMPACT
        PlayMusic(oggDying, true, 0.3f, 1.0f, 1, false);
        EnsureBrandishIsOff("");
        GiveSanityDamage(1.0f, true);
        FadeRadialBlurTo(0.05f, 1.0f);
        PlaySoundAtEntity("bell", sntBell, "Player");
        PlaySoundAtEntity("knife1", sntCut, "Player");
        CutAsync("");
        SetPlayerHealth(25.0f);
        delay = 2.0f;
    }
    else if(stage == 16)
    {
        PlayVoice("RC_Johan_3.ogg");
        SetEffectVoiceOverCallback("TortureDialog");
        return;
    }
    else if(stage == 17)
    {
        SetLocalVarInt("StopCutAsync", 1);
        RemoveTimer("CutTimer");
        delay = 2.0f;
    }
    else if(stage == 18)
    {
        SetLocalVarInt("StopCutAsync", 0);
        PlayVoice("RC_Anton_4.ogg", "AntonVoiceArea");
        SetEffectVoiceOverCallback("TortureDialog");
        return;
    }
    else if(stage == 19)
    {
        ///FURTHER STAB SOUND
        CutAsync("");
        FadeOut(3.0f);
        delay = 6.0f;
    }
    else if(stage == 20)
    {
        StopSound("knife1", 0.5f);
        StopMusic(1, 1.0f);
        SetLocalVarInt("StopCutAsync", 1);
        RemoveTimer("CutTimer");
        SetPlayerHealth(100.0f);
        StopSound("bell", 1.0f);
        delay = 8.0f;
    }
    else if(stage == 21)
    {
        PlayVoice("IN_Johan_0.ogg");
        SetEffectVoiceOverCallback("TortureDialog");
        return;
    }
    else if(stage == 22)
    {
        SetPlayerCrouching(true);
        MovePlayerHeadPos(0.0f, -1.0f, 0.0f, 20.0f, 0.5f);
        FadePlayerRollTo(20.0f, 20.0f, 20.0f);
        FadeGlobalSoundVolume(0.0f, 1.0f);
        delay = 4.0f;
    }
    else
    {
        StopPlayerLookAt();
        TeleportPlayer("PlayerStartArea_1");
        WakeUpDialog();
        return;
    }
    AddTimer("", delay, "TortureDialog");
}

void CutAsync(string t)
{
    PlaySoundAtEntity(sntInject, "Player");
    GiveSanityDamage(0.0f, true);
    PlaySoundAtEntity(sntCloth, entAntonArea);
    FadePlayerRollTo(RandFloat(-20.0f, -10.0f), 20.0f, 20.0f);
    FadePlayerFOVMulTo(RandFloat(0.7f, 1.0f), 20.0f);
    if(GetLocalVarInt("StopCutAsync") == 1) { return; }
    AddTimer("CutTimer", RandFloat(5.0f, 8.0f), "CutAsync");
}

void TortureDialog()
{
    TortureDialog("");
}

void EnsureBrandishIsOff(string t)
{
    int stage = GetLocalVarInt("BrandishOff");
    AddLocalVarInt("BrandishOff", 1);
    if(stage < 20)
    {
        StopSound("brandish", 0.5f);
        AddTimer("", 0.2f, "EnsureBrandishIsOff");
    }
}

// ----------------------------------------------------------------------------
// After-Cutscene Dialog
// ----------------------------------------------------------------------------
void WakeUpDialog(string t)
{
    int stage = GetLocalVarInt("WakeUpDialog");
    AddLocalVarInt("WakeUpDialog", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        FadeGlobalSoundVolume(1.0f, 0.5f);
        FadeIn(8.0f);
        FadeRadialBlurTo(0.03f, 1.0f);
        FadeImageTrailTo(0.015f, 1.0f);
        HeadTilt("");
        delay = 5.0f;
    }
    else if(stage == 1)
    {
        PlayVoice("RC_Johan_4.ogg");
        SetEffectVoiceOverCallback("WakeUpDialog");
        return;
    }
    else if(stage == 2)
    {
        PlayVoice("RC_Johan_5.ogg");
        SetEffectVoiceOverCallback("WakeUpDialog");
        return;
    }
    else if(stage == 3)
    {
        FadeOut(0.3f);
        delay = 0.2f;
    }
    else if(stage == 4)
    {
        PlaySoundAtEntity(sntCloth, "Player");
        MovePlayerHeadPos(0.0f, 0.0f, 0.0f, 2.0f, 0.5f);
        delay = 1.2f;
    }
    else if(stage == 5)
    {
        FadeIn(0.3f);
        ResetPlayerSpeed();
        SetPlayerActive(true);
    }
    else
    {
        return;
    }

    AddTimer("", delay, "WakeUpDialog");
}

void WakeUpDialog()
{
    WakeUpDialog("");
}

// ----------------------------------------------------------------------------
// Introducing the Item Quest (Should be expanded to tie into the area box by the torture room door)
// ----------------------------------------------------------------------------

void StartDialog(string a, string b, int s)
{
    QuestDialog();
    GiveQuestIfNotAdded("RisenQuest");
}

void QuestDialog(string t)
{
    int stage = GetLocalVarInt("QuestDialog");
    AddLocalVarInt("QuestDialog", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        PlayVoice("RC_Johan_6.ogg");
        SetEffectVoiceOverCallback("QuestDialog");
        return;
    }
    else if(stage == 1)
    {
        PlayVoice("RC_Johan_6b.ogg");
        SetEffectVoiceOverCallback("QuestDialog");
        return;
    }
    else
    {
        return;
    }

    AddTimer("", delay, "QuestDialog");
}

void QuestDialog()
{
    QuestDialog("");
}

// ----------------------------------------------------------------------------
// PRIORY CODE AREA
// ----------------------------------------------------------------------------

void StartPriory(string a, string b, int s)
{
    PrioryDialog();
}

void PrioryDialog(string t)
{
    int stage = GetLocalVarInt("PrioryDialog");
    AddLocalVarInt("PrioryDialog", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        PlayVoice("RC_Johan_7.ogg");
        PlayGuiSound("01_event_dust.ogg", 0.2f);
        SetEffectVoiceOverCallback("PrioryDialog");
        return;
    }
    else if(stage == 1)
    {
        PlaySoundAtEntity(sntPiano, entPianoArea);
    }
    else
    {
        return;
    }

    AddTimer("", delay, "PrioryDialog");
}

void PrioryDialog()
{
    PrioryDialog("");
}

// ============================================================================
// COLLECTING REQUIRED ITEMS
// ============================================================================
const int requiredItemsCount = 2;

bool ItemIsRequired(string itemName)
{
    return itemName == entRisenWater ||
    itemName == entRisenSalt;
}

void OnRequiredItemPickup(string entity)
{
    // Ignore not required items
    if (!ItemIsRequired(entity)) { return; }

    if (entity == entRisenWater)        { OnWaterPickedUp(); }
    if (entity == entRisenSalt)         { OnSaltPickedUp(); }

    AddLocalVarInt("RequiredItemsPickedUp", 1);
    GiveQuestIfNotAdded("RisenQuest");

    if (AllRequiredItemsPickedUp())
    {
        OnAllItemsPickedUp();
    }
}

bool AllRequiredItemsPickedUp()
{
    return GetLocalVarInt("RequiredItemsPickedUp") == requiredItemsCount;
    CompleteQuest("RisenQuest");
}

// ============================================================================
// REQUIRED ITEMS PICK UP EVENTS
// ============================================================================

void OnWaterPickedUp()
{
        PlayVoice("RC_Johan_10.ogg");
}

void OnSaltPickedUp()
{
        PlayVoice("RC_Johan_12.ogg");
}

void OnAllItemsPickedUp()
{
    CompleteQuest("RisenQuest");
    AddSinglePlayerCollideCallback("EndingCutsceneTrigger", "StartEnding");
}

void StartEnding(string a, string b, int s)
{
    EndingDialog();
    SetPlayerActive(false);
    StartPlayerLookAt("LookAtDoor", 0.8f, 1.5f, "");
    SetSwingDoorLocked("BlowOpenDoor1", false, true);
}

void EndingDialog(string t)
{
    int stage = GetLocalVarInt("EndingDialog");
    AddLocalVarInt("EndingDialog", 1);
    float delay = 0.0f;

    if(stage == 0)
    {
        PlayVoice("RC_Johan_13.ogg");
        SetEffectVoiceOverCallback("EndingDialog");
        return;
    }
    else if(stage == 1)
    {
        PlayVoice("RC_Anton_5.ogg");
        SetEffectVoiceOverCallback("EndingDialog");
        return;
    }
    else if(stage == 2)
    {
        delay = 1.0f;
    }
    else if(stage == 3)
    {
        PlayVoice("RC_Anton_6.ogg");
        SetEffectVoiceOverCallback("EndingDialog");
        return;
    }
    else if(stage == 4)
    {
        delay = 1.0f;
        ///PRISONER DIALOG HERE
    }
    else if(stage == 5)
    {
        PlayVoice("RC_Anton_7.ogg");
        SetEffectVoiceOverCallback("EndingDialog");
        return;
    }
    else if(stage == 5)
    {
        ///SOUNDS OF CRASHING RUBBLE IN DISTANCE, ECHOING LAUGHTER
        delay = 1.0f;
    }
    else if(stage == 6)
    {
        StopPlayerLookAt();
        SetPlayerActive(true);
    }
    else
    {
        return;
    }

    AddTimer("", delay, "EndingDialog");
}

void EndingDialog()
{
    EndingDialog("");
}


// ============================================================================
// LEFT HAND PATH HORROR AREAS
// ============================================================================

void StartHorror(string a, string b, int s)
{
    PlayVoiceOnPlayerAndSlowdown("RC_Johan_11.ogg");
}
